# Maintainer: Nicholas Novak <34256932+NickyBoy89@users.noreply.github.com>

pkgname=typst-git
_pkgname=${pkgname%-git}
name=typst
pkgver=0.11.0.r25.g1e645b396
pkgrel=1
pkgdesc='A markup-based typesetting system for the sciences'
license=('Apache-2.0')
arch=('any')
_url="https://github.com/$_pkgname/$_pkgname"
makedepends=(cargo
			 git)
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
depends=(gcc-libs
         glibc)
url="https://github.com/typst/typst"
source=("$_pkgname::git+$_url.git")
md5sums=('SKIP')

prepare() {
	cd "$_pkgname"
	_srcenv
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

pkgver() {
	cd "$_pkgname"
	git describe --long --tags |
		sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g;s/rc.\.//'
}


_srcenv() {
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
}

build() {
  cd "$_pkgname"
  _srcenv
CFLAGS+=" -ffat-lto-objects"
export TYPST_VERSION="${pkgver}"
export GEN_ARTIFACTS=artifacts/
  cargo build -p typst-cli --frozen --release --all-features
}

package() {
	cd "$_pkgname"
	local _artifacts='crates/typst-cli/artifacts'
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$_pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/man/man1/" "$_artifacts/$_pkgname"*.1
	install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" "$_artifacts/_$_pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" "$_artifacts/$_pkgname.fish"
	install -Dm0644 "$_artifacts/$_pkgname.bash" "$pkgdir/usr/share/bash-completion/completions/$_pkgname"
}

