# Maintainer: Nicholas Novak <34256932+NickyBoy89@users.noreply.github.com>

pkgname=typst-git
name=typst
pkgver=r2346.d7928a8e
pkgrel=1
pkgdesc="A new markup-based typesetting system that is powerful and easy to learn. "
license=('Apache-2.0')
arch=('any')
makedepends=('git' 'cargo')
provides=('typst')
depends=('gcc-libs' 'glibc')
url="https://github.com/typst/typst"
source=(
	"typst::git+https://github.com/typst/typst"
)
md5sums=('SKIP')

prepare() {
	cd "$srcdir/typst"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}


pkgver() {
	cd "$srcdir/typst"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/typst"
	CFLAGS+=" -ffat-lto-objects"
	export GEN_ARTIFACTS=artifacts/
  cargo build -p typst-cli --release --all-features
}

package() {
	# Install the binary
	install -vDm 755 "$srcdir/typst/target/release/typst" "$pkgdir/usr/bin/typst"

  # Install the licenses
  install -vDm 644 "$srcdir/typst/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	# Install man pages and completions
	local _artifacts="$srcdir/typst/crates/typst-cli/artifacts"
	install -Dm0644 -t "$pkgdir/usr/share/man/man1/" "$_artifacts/$name"*.1
	install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" "$_artifacts/_$name"
	install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" "$_artifacts/$name.fish"
	install -Dm0644 "$_artifacts/$name.bash" "$pkgdir/usr/share/bash-completion/completions/$name"
}

